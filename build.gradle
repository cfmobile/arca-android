
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

ext.projects = [
        project(':arca-adapters'),
        project(':arca-dispatcher'),
        project(':arca-fragments'),
        project(':arca-monitor'),
        project(':arca-broadcaster'),
        project(':arca-provider'),
        project(':arca-service'),
        project(':arca-threading'),
        project(':arca-utils')
]

ext.pomInfo = {
    name 'Arca Android'
    description 'A comprehensive framework for caching and displaying data.'
    url 'https://github.com/cfmobile/arca-android'

    scm {
        url 'git@github.com:cfmobile/arca-android.git'
        connection 'scm:git:git@github.com:cfmobile/arca-android.git'
        developerConnection 'scm:git:git@github.com:cfmobile/arca-android.git'
    }

    licenses {
        license {
            name 'Modified BSD License'
            url 'http://opensource.org/licenses/BSD-3-Clause'
        }
    }

    developers {
        developer {
            id 'jwinters'
            name 'Joshua Winters'
            email 'jwinters@pivotal.io'
        }
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'io.pivotal'

    version = '1.1.3'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {

            pom.project pomInfo

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        }
    }
}


configure(projects) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 28
        buildToolsVersion "28.0.3"

        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 28
        }

        sourceSets {
            androidTest.setRoot('src/test')
        }
    }
}