apply plugin: 'java'

dependencies {
    compile project(":arca-app")
    compile project(":arca-core")
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

ext.pomProjects = [
        rootProject,
        project('arca-app'),
        project('arca-core')
]

ext.libProjects = [
        project('arca-app:arca-adapters'),
        project('arca-app:arca-dispatcher'),
        project('arca-app:arca-fragments'),
        project('arca-app:arca-monitor'),
        project('arca-core:arca-broadcaster'),
        project('arca-core:arca-provider'),
        project('arca-core:arca-service'),
        project('arca-core:arca-threading'),
        project('arca-core:arca-utils')
]

ext.pomInfo = {
    name 'Arca Android'
    description 'A comprehensive framework for caching and displaying data.'
    url 'https://github.com/cfmobile/arca-android'

    scm {
        url 'git@github.com:cfmobile/arca-android.git'
        connection 'scm:git:git@github.com:cfmobile/arca-android.git'
        developerConnection 'scm:git:git@github.com:cfmobile/arca-android.git'
    }

    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'jwinters'
            name 'Joshua Winters'
            email 'jwinters@pivotal.io'
        }
    }
}


allprojects {
    apply plugin: 'maven'

    group = 'io.pivotal'
    version = '1.0-beta.4-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    uploadArchives {
        repositories.mavenDeployer {

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }
        }
    }
}

configure(pomProjects) {
    apply plugin: 'java'

    uploadArchives {
        repositories.mavenDeployer {
            pom.project pomInfo

            pom.whenConfigured {
                pom -> pom.packaging = 'pom'
            }
        }
    }
}

configure(libProjects) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 21
        buildToolsVersion "21.1.2"

        defaultConfig {
            minSdkVersion 9
            targetSdkVersion 21
            versionCode 1
            versionName "1.0"
        }

        sourceSets {
            androidTest.setRoot('src/test')
        }
    }

    task jarClasses(type: Jar, dependsOn: assembleRelease) {
        from fileTree(dir: 'build/intermediates/classes/release')
    }

    task jarSources(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task jarJavadoc(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jarClasses
        archives jarSources
        archives jarJavadoc
    }

    uploadArchives {
        repositories.mavenDeployer {
            pom.project pomInfo

            pom.whenConfigured {
                pom -> pom.packaging = 'jar'
            }
        }
    }
}