
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

ext.projects = [
        project('arca-app:arca-adapters'),
        project('arca-app:arca-dispatcher'),
        project('arca-app:arca-fragments'),
        project('arca-app:arca-monitor'),
        project('arca-core:arca-broadcaster'),
        project('arca-core:arca-provider'),
        project('arca-core:arca-service'),
        project('arca-core:arca-threading'),
        project('arca-core:arca-utils')
]

ext.pomInfo = {
    name 'Arca Android'
    description 'A comprehensive framework for caching and displaying data.'
    url 'https://github.com/cfmobile/arca-android'

    scm {
        url 'git@github.com:cfmobile/arca-android.git'
        connection 'scm:git:git@github.com:cfmobile/arca-android.git'
        developerConnection 'scm:git:git@github.com:cfmobile/arca-android.git'
    }

    licenses {
        license {
            name 'Modified BSD License'
            url 'http://opensource.org/licenses/BSD-3-Clause'
        }
    }

    developers {
        developer {
            id 'jwinters'
            name 'Joshua Winters'
            email 'jwinters@pivotal.io'
        }
    }
}

allprojects {
    apply plugin: 'maven'

    group = 'io.pivotal'
    version = '1.0-beta.7-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    uploadArchives {
        repositories.mavenDeployer {

            // Publishes AAR to local Maven cache:
//            repository(url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository/').absolutePath)

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }
        }
    }
}

configure(projects) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.1"

        defaultConfig {
            minSdkVersion 9
            targetSdkVersion 23
        }

        sourceSets {
            androidTest.setRoot('src/test')
        }
    }

    task jarClasses(type: Jar, dependsOn: assembleRelease) {
        from fileTree(dir: 'build/intermediates/classes/release')
    }

    task jarSources(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives jarClasses
        archives jarSources
    }

    uploadArchives {
        repositories.mavenDeployer {
            pom.project pomInfo

            pom.whenConfigured {
                pom -> pom.packaging = 'jar'
            }
        }
    }
}