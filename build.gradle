
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

ext.projects = [
        project(':arca-adapters'),
        project(':arca-dispatcher'),
        project(':arca-fragments'),
        project(':arca-monitor'),
        project(':arca-broadcaster'),
        project(':arca-provider'),
        project(':arca-service'),
        project(':arca-threading'),
        project(':arca-utils')
]

ext.pomInfo = {
    name 'Arca Android'
    description 'A comprehensive framework for caching and displaying data.'
    url 'https://github.com/cfmobile/arca-android'

    scm {
        url 'git@github.com:cfmobile/arca-android.git'
        connection 'scm:git:git@github.com:cfmobile/arca-android.git'
        developerConnection 'scm:git:git@github.com:cfmobile/arca-android.git'
    }

    licenses {
        license {
            name 'Modified BSD License'
            url 'http://opensource.org/licenses/BSD-3-Clause'
        }
    }

    developers {
        developer {
            id 'jwinters'
            name 'Joshua Winters'
            email 'jwinters@pivotal.io'
        }
    }
}

allprojects {
    apply plugin: 'maven'

    group = 'io.pivotal'
    version = '1.0-beta.16-SNAPSHOT'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    uploadArchives {
        repositories.mavenDeployer {

            // Publishes AAR to local Maven cache:
            repository(url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository/').absolutePath)

//            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
//                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
//            }
//
//            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
//                authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
//            }
        }
    }
}

configure(projects) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 27
        buildToolsVersion "27.0.1"

        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 27
        }

        sourceSets {
            androidTest.setRoot('src/test')
        }
    }
}